---
layout: post
title:  "Clojure 1.9 introduces clojure.spec: tutorial with live coding examples #cljklipse @viebel"
description:  "spec tutorial clojure klipse live coding examples clojurescript cljs"
date:   2016-05-30 18:17:52 +0200
categories: clojure
thumbnail: assets/klipse.png
guid: "EB8AA161-9E33-41DE-B35D-4F2FF2B973D4"
author: "@viebel"

---

# What is Spec?

The `spec` library specifies the structure of data, validates or destructures it, and can generate data based on the spec. The `clojure.spec` (or `cljs.spec`) namespace is included in the Clojure core distribution, so no extra library is required to use it. `spec` will be part of in Clojure` 1.9.


In this article, we present `spec` live coding examples provided by [KLIPSE][app-url]. (This article is a rewrite of [http://clojure.org/guides/spec](http://clojure.org/guides/spec) with live examples instead of static code snippets.)

Get prepared to experience something really amazing, enabled by Self-Host Clojurescript: At run time, KLIPSE will load the code from the `clojurecript` [gihub repository](https://github.com/clojure/clojurescript/commits/master) where the code for `spec` has been ported on May 29 by David Nolen and Mike Fikes.

![Github](/assets/github.jpg)

Because, all the magic (code loaded from github + execution) happens at run time, it takes about 15 seconds to execute all the code snippets of this page (sometimes the page might seem to hang :(). So please be patient and do not read too fast.


All the coding examples below are **live** and **editable**:

- the code is executed in your browser while you are reading
- you can edit the code and after 2 seconds it is evaluated

# Setup

Let's start by requiring the `cljs.spec` namespace: 

<pre class="language-klipse"  src-paths="http://app.klipse.tech/fig/js,clojurescript">
<code>

(ns my.spec
  (:require [cljs.spec :as s]))
(ns my.spec
  (:require [cljs.spec :as s]))
</code></pre>

For a technical reason, related to macro definition in self-host, we have to require `cljs.spec` twice.

# Predicates

Each spec describes a set of allowed values. There are several ways to build specs and all of them can be composed to build more sophisticated specs.

Any existing Clojure function that takes a single argument and returns a truthy value is a valid predicate spec. We can check whether a particular data value conforms to a spec using conform:

~~~klipse
(s/conform even? 1000)
~~~

The `conform` function takes something that can be a spec and a data value. Here we are passing a predicate which is implicitly converted into a spec. The return value is "conformed". Here, the conformed value is the same as the original value - we’ll see later where that starts to deviate. If the value does not conform to the spec, the special value `:clojure.spec/invalid` is returned.

~~~klipse
(s/conform even? 999)
~~~

If you don’t want to use the conformed value or check for `:clojure.spec/invalid`, the helper `valid?` can be used instead to return a boolean.

~~~klipse
(s/valid? even? 10)
~~~

Note that again `valid?` implicitly converts the predicate function into a spec. The spec library allows you to leverage all of the functions you already have - there is no special dictionary of predicates. Some more examples:

~~~klipse
(s/valid? nil? nil)
~~~

~~~klipse
(s/valid? string? "abc")
~~~

~~~klipse
(s/valid? #(> % 5) 10)
~~~

~~~klipse
(s/valid? #(> % 5) 0)
~~~

Sets can also be used as predicates that match one or more literal values:

~~~klipse
(s/valid? #{:club :diamond :heart :spade} :club) 
~~~

# Registry

Until now, we’ve been using specs directly. However, spec provides a central registry for globally declaring reusable specs. The registry associates a namespaced keyword with a specification. The use of namespaces ensures that we can define reusable non-conflicting specs across libraries or applications.

Specs are registered using def. It’s up to you to register the specification in a namespace that makes sense (typically a namespace you control).

~~~klipse
(s/def ::date #(instance? js/Date %))
(s/def ::suit #{:club :diamond :heart :spade})
~~~

A registered spec identifier can be used in place of a spec definition in the operations we’ve seen so far - conform and valid?.

~~~klipse
(s/valid? ::date (js/Date.))
~~~

~~~klipse
(s/conform ::suit :club)
~~~

You will see later that registered specs can (and should) be used anywhere we compose specs.

# Composing predicates

The simplest way to compose specs is with [and](http://clojure.github.io/clojure/branch-master/clojure.spec-api.html#clojure.spec/and) and [or](http://clojure.github.io/clojure/branch-master/clojure.spec-api.html#clojure.spec/or). Let’s create a spec that combines several predicates into a composite spec with `s/and`:

~~~klipse
(s/def ::big-even (s/and integer? even? #(> % 1000)))
(s/valid? ::big-even :foo)
~~~

~~~klipse
(s/valid? ::big-even 10)
~~~

~~~klipse
(s/valid? ::big-even 100000)
~~~

We can also use `s/or` to specify two alternatives:

~~~klipse
(s/def ::name-or-id (s/or :name string?
                          :id   integer?))
(s/valid? ::name-or-id "abc") 
~~~

~~~klipse
(s/valid? ::name-or-id 100)
~~~

~~~klipse
(s/valid? ::name-or-id :foo) 
~~~


This `or` spec is the first case we’ve seen that involves a choice during validity checking. Each choice is annotated with a tag (here, `:name` and `:id`) and those tags give the branches names that can be used to understand or enrich the data returned from conform and other spec functions.

When an `or` is conformed, it returns a vector with the tag name and conformed value:

~~~klipse
(s/conform ::name-or-id "abc")
~~~

~~~klipse
(s/conform ::name-or-id 100)
~~~

Many predicates that check an instance’s type do not allow `nil` as a valid value (`string?`, `number?`, `keyword?`, etc...). To include `nil` as a valid value, use the provided function [nilable](http://clojure.github.io/clojure/branch-master/clojure.spec-api.html#clojure.spec/nilable) to make a spec:

~~~klipse
(s/valid? string? nil)
~~~

~~~klipse
(s/valid? (s/nilable string?) nil)
~~~

# Explain

[explain](http://clojure.github.io/clojure/branch-master/clojure.spec-api.html#clojure.spec/explain), [explain-str](http://clojure.github.io/clojure/branch-master/clojure.spec-api.html#clojure.spec/explain-str) and [explain-data](http://clojure.github.io/clojure/branch-master/clojure.spec-api.html#clojure.spec/explain-data) are another high-level operations in `spec` that can be used to report why a value does not conform to a spec. Let’s see it in action with some non-conforming examples we've seen so far:

~~~klipse
(with-out-str
(s/explain ::suit 42))
~~~

~~~klipse
(s/explain-data ::big-even 5)
~~~

~~~klipse
(s/explain-str ::name-or-id :foo)
~~~


The `explain` output identifies the problematic value and the predicate it was evaluating. In the last example we see that when there are alternatives, errors across all of the alternatives will be printed.

This is just the beginning of what is possible with `spec`. In an upcoming article, we will present more cool features of `spec`:

- Sequences
- Entity maps
- Multi-spec
- Collections
- Validations
- Functions
- Macros
- Conformers


Clojurescript rocks!

[app-url]: http://app.klipse.tech?blog=klipse

